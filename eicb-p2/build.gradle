apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

compileJava.sourceCompatibility = '1.8'

sourceSets {
	// make the testcases directory the default resources tree for the 'test' source set.
	test.resources.srcDirs = ['src/test/testcases']
}

dependencies {
	implementation 'commons-io:commons-io:2.4'
	implementation 'commons-cli:commons-cli:1.3.1'
	implementation 'com.thoughtworks.xstream:xstream:1.4.9'
	implementation 'org.xmlunit:xmlunit-parent:2.2.1'
	implementation 'org.xmlunit:xmlunit-core:2.2.1'
	implementation 'org.xmlunit:xmlunit-matchers:2.2.1'

	testImplementation 'junit:junit:4.12'
}

task prepareSubmission(type: Zip, dependsOn: classes) {
	description = "Prepare your solution for submission via Moodle"
	
	def banner = "############################################################"
	doFirst {
		if(!project.hasProperty("GroupNumber") || GroupNumber == "_not_set_")
			throw new GradleException("""${banner}
				| Group number required for submission!
				|
				| Run this task as
				|   \$ gradle prepareSubmission -PGroupNumber=42
				| or set your group number in the file 'gradle.properties'
				|${banner}""".stripMargin())

		archiveBaseName = "eicb-p2-submission-g${GroupNumber}"
	}
	
	outputs.dir('build/distributions')
	
	from(sourceSets.main.java) {
		include('mavlc/context_analysis/ConstantExpressionEvaluator.java')
		include('mavlc/context_analysis/ContextualAnalysis.java')
		include('mavlc/context_analysis/IdentificationTable.java')
		into('src/main/java')
	}
	
	doLast {
		println banner
		println " Please submit"
		println "   " + project.relativePath(archivePath)
		println " via Moodle"
		println banner
	}
}

apply from: 'scripts.gradle'
